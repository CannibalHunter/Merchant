version "4.5"

#include "ZSCRIPT.MerchantMenu.zsc"
#include "ZSCRIPT.StoreItems.zsc"
#include "ZScript/MerchZForms/Include.zsc" // [Ace] For the menu.

class MerchantHandler : EventHandler
{
	Array<StoreItem> StoreItems;
	Array<string> Categories;
	private int CashFromMonsters;

	override void NetworkProcess(ConsoleEvent e)
	{
		let plr = players[e.Player].mo;
		Array<string> ParsedName;
		e.Name.Split(ParsedName, ":");
		if (ParsedName[0] ~== "HDM_BuyStuff")
		{
			StoreItem Item;
			for (int i = 0; i < StoreItems.Size(); ++i)
			{
				if (StoreItems[i].GetClass() == ParsedName[1])
				{
					Item = StoreItems[i];
					break;
				}
			}

			if (Item && plr)
			{
				class<Actor> ItemClass = Item.GetItem();
				int Amount = Item.GetAmount();

				int Currency = e.Args[0];
				class<Inventory> CurrencyClass = StoreItem.GetCurrencyClass(Currency);
				int Price = Item.GetAdjustedPrice(Currency);

				if (Price > plr.CountInv(CurrencyClass))
				{
					plr.A_StartSound("Merchant/BuyFailure", 15, CHANF_LOCAL | CHANF_UI);
					return;
				}

				if (ItemClass is "HDWeapon" || ItemClass is "HDUPK")
				{
					plr.A_SpawnItemEx(ItemClass, 6, 0, plr.height - 8, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(2.0, 5.0), 0, SXF_NOCHECKPOSITION);
				}
				else
				{
					class<Inventory> InvClass = (class<Inventory>)(ItemClass);
					int MaxGive = GetDefaultByType(InvClass).MaxAmount - plr.CountInv(InvClass);
					int MaxActualGive = min(Amount, MaxGive);

					// [Ace] Give what you can directly and drop the rest on the ground.
					plr.A_GiveInventory(InvClass, MaxActualGive);
					Amount -= MaxActualGive;
					for (int i = 0; i < Amount; ++i)
					{
						plr.A_SpawnItemEx(InvClass, 6, 0, plr.height - 8, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(2.0, 5.0), 0, SXF_NOCHECKPOSITION);
					}
				}
				plr.A_TakeInventory(CurrencyClass, Price);
				plr.A_StartSound("Merchant/BuySuccess", 16, CHANF_LOCAL | CHANF_UI);
			}
		}

		if (e.Name ~== "HDM_ResetStore")
		{
			InitStore();
		}
	}

	override void WorldThingDamaged(WorldEvent e)
	{
		string FollowerClassName = "HDFollower";
		class<Actor> FollowerClass = FollowerClassName;
		if (!e.Thing || e.Thing.Health > 0 || e.Thing.bFRIENDLY || !e.Thing.bISMONSTER || !(e.DamageSource is "HDPlayerPawn") && FollowerClass && !(e.DamageSource is Actor.GetReplacement(FollowerClass)))
		{
			return;
		}

		double Reward = 0;
		if (!(e.Thing is "HDMobBase"))
		{
			Reward = ceil(e.Thing.GetSpawnHealth() / 200.0); // [Ace] Not perfect, but it'll do.
		}
		else if (e.Thing is "ZombieStormTrooper" || e.Thing is "HideousShotgunGuy" || e.Thing is "Babuin" || e.Thing is "UndeadHomeboy")
		{
			Reward = 1;
		}
		else if (e.Thing is "Serpentipede" || e.Thing is "VulcanetteGuy")
		{
			Reward = 1.25;
		}
		else if (e.Thing is "HDMarine" || e.Thing is "NinjaPirate" || e.Thing is "Yokai")
		{
			Reward = 1.75;
		}
		else if (e.Thing is "TechnoSpider" || e.Thing is "CombatSlug" || e.Thing is "Boner" || e.Thing is "Trilobite")
		{
			Reward = 2.5;
		}
		else if (e.Thing is "PainBringer" || e.Thing is "HoopBubble")
		{
			Reward = 4;
		}
		else if (e.Thing is "PainLord")
		{
			Reward = 7;
		}
		else if (e.Thing is "CommanderKeen" && !random(0, 2))
		{
			Reward = 66.666;
		}
		else if (e.Thing is "Technorantula")
		{
			Reward = 25;
		}
		else if (e.Thing is "SatanRobo")
		{
			Reward = 50;
		}
		else if (e.Thing is "Necromancer")
		{
			Reward = 75;
		}
		else if (e.Thing is "HDBossBrain")
		{
			Reward = 500;
		}

		if (Reward > 0)
		{
			int Amount = int(Reward);
			if (frandom[cashrand](0.01, 1.00) < Reward % 1.0)
			{
				Amount++;
			}
			CashFromMonsters += Amount;
		}
	}

	private void InitStore()
	{
		StoreItems.Clear();
		Categories.Clear();
		
		for (int i = 0; i < AllClasses.Size(); ++i)
		{
			if (AllClasses[i] is "StoreItem" && AllClasses[i].GetClassName() != 'StoreItem')
			{
				StoreItem Item = StoreItem(new(AllClasses[i]));
				class<Actor> a = Item.GetItem();
				if (a)
				{
					StoreItems.Push(Item);
				}
			}
		}

		for (int i = 0; i < StoreItems.Size(); ++i)
		{
			// [Ace] Don't duplicate categories.
			if (Categories.Find(StoreItems[i].GetCategory()) == Categories.Size())
			{
				Categories.Push(StoreItems[i].GetCategory());
			}
		}

		// [Ace] Sort by category index.
		for (int i = 0; i < StoreItems.Size() - 1; ++i)
		{
			for (int j = i + 1; j < StoreItems.Size(); ++j)
			{
				string C1, C2; int I1, I2;
				[C1, I1] = StoreItems[i].GetCategory();
				[C2, I2] = StoreItems[j].GetCategory();

				if (I1 > I2)
				{
					let Swap = StoreItems[i];
					StoreItems[i] = StoreItems[j];
					StoreItems[j] = Swap;
				}
			}
		}
	}

	override void WorldLoaded(WorldEvent e)
	{
		InitStore();

		for (int i = 0; i < level.Sectors.Size(); ++i)
		{
			Sector CurrSec = level.Sectors[i];
			vector3 SpawnPos = (CurrSec.centerspot.x, CurrSec.centerspot.y, CurrSec.floorplane.ZAtPoint(CurrSec.centerspot));

			if (CurrSec.IsSecret() && random(1, 100) <= max(8, 20 - 2 * level.total_secrets))
			{
				Actor.Spawn("AceMerchant", SpawnPos);
				break;
			}
		}
	}

	override void WorldUnloaded(WorldEvent e)
	{
		double TotalCash = CashFromMonsters + level.found_secrets * 15 - Thinker.Tics2Seconds(level.time) / 60;
		int MoneyPerPlayer = int(TotalCash / GetPlayersInGame());

		for (int i = 0; i < MAXPLAYERS; ++i)
		{
			if (players[i].mo && MoneyPerPlayer > 0)
			{
				players[i].mo.A_GiveInventory("MercenaryBucks", MoneyPerPlayer);
			}
		}
	}

	private int GetPlayersInGame()
	{
		int TotalPlayers = 0;
		for (int i = 0; i < MAXPLAYERS; ++i)
		{
			if (playeringame[i])
			{
				TotalPlayers++;
			}
		}
		return TotalPlayers;
	}
}

// ----------------------------------------
// MERCHANT
// ----------------------------------------

class AceMerchant : Actor
{
	override void Tick()
	{
		if (CheckProximity("PlayerPawn", 96, 1, CPXF_ANCESTOR | CPXF_SETTARGET))
		{
			Angle = AngleTo(target);
		}

		Super.Tick();
	}

	override bool Used(Actor user)
	{
		if (user.PlayerNumber() == consoleplayer)
		{
			Menu.SetMenu('MerchantShopMenu');
		}

		return Super.Used(user);
	}

	override bool CanCollideWith(Actor other, bool passive)
	{
		if (other is "PlayerPawn")
		{
			return Super.CanCollideWith(other, passive);
		}

		return false;
	}

	Default
	{
		Mass 100;
		Height 58;
		Radius 8;
		Scale 1.0;
		+SOLID
		+PUSHABLE
		+NOBLOCKMONST
	}

	States
	{
		Spawn:
			AMRC # -1 NoDelay
			{
				frame = CVar.GetCVar("hdm_skin", players[consoleplayer]).GetInt();
			}
			Stop;
	}
}

class MercenaryBucks : HDAmmo
{
	override void SplitPickup()
	{
		int SplitAmount = min(Amount, random(20, 100));
		while (Amount > SplitAmount)
		{
			int SubSplit = min(Amount, random(20, 100));
			Actor a = Spawn("MercenaryBucks", pos);
			a.vel += vel + (frandom(-1, 1), frandom(-1, 1), frandom(-1, 1));
			Scale.X *= randompick(-1, 1);
			Inventory(a).Amount = SubSplit;
			Amount -= SubSplit;
		}

		if (amount < 1)
		{
			Destroy();
			return;
		}
	}

	override bool IsUsed()
	{
		return true;
	}

	Default
	{
		+FORCEXYBILLBOARD
		Tag "Bucks";
		Inventory.Icon "MBCKA0";
		+HDPICKUP.MULTIPICKUP
		+HDPICKUP.FITSINBACKPACK
		-HDPICKUP.DROPTRANSLATION
		HDPickup.Bulk 0.06;
		HDPickup.RefID "bux";
		Inventory.MaxAmount 10000;
		Inventory.PickupMessage "Picked up some bucks.";
		Scale 0.4;
	}

	States
	{
		Spawn:
			MBCK A -1;
			Stop;
	}
}