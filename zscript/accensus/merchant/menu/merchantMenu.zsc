class MerchantShopMenu : MerchZFGenericMenu {

    MerchantHandler Handler;
    int SelCategoryIndex;
    int SelCurrencyIndex;

    const Spacing = 3;

    Array<MerchantCategoryFrame> catFrames;
    MerchZFListFrame shopContent;

    override void Init(Menu parent) {
        Super.Init(parent);

        menuactive = Menu.OnNoPause;
        Handler = MerchantHandler(EventHandler.Find("MerchantHandler"));


        vector2 virtRes = (640, 480);
        SetBaseResolution(virtRes);

        vector2 basePos = (virtRes.x * 0.1, 10);
        vector2 sSize = (virtRes.x - basePos.x * 2, virtRes.y - SmallFont.GetHeight() + (Spacing * 2.0));
        BuildShopFrame(basePos, sSize);
    }

    static clearscope double GetBoxedScale(vector2 size, vector2 boxSize) {
        return max(size.x / boxSize.x, size.y / boxSize.y);
    }

    ui MerchantStore GetOpenedStore() {
        return Handler.Stores[Handler.OpenedStoreIndex[consoleplayer]];
    }

    ui string GetSelectedCategory() {
        let openedStore = GetOpenedStore();
        return SelCategoryIndex < openedStore.Categories.Size() ? openedStore.Categories[SelCategoryIndex] : "Unknown Category";
    }

    ui void GetItemsInCategory(out Array<StoreItem> items, string category) {
        items.Clear();

        // Add all items in the current category that aren't the current currency.
        foreach (storeItem : GetOpenedStore().storeItems) {
            if (
                storeItem.GetCategory() == category
                && storeItem.GetItem() != Handler.StoreCurrencies[SelCurrencyIndex].GetCurrencyClass()
            ) items.Push(storeItem);
        }
    }

    private ui void BuildShopFrame(vector2 pos, vector2 size) {

        shopContent = MerchZFListFrame.create((0,0), (size.x, 0), Spacing * 2.0);

        let totalItems = 0;
        foreach (category : GetOpenedStore().Categories) {

            Array<StoreItem> items;
            GetItemsInCategory(items, category);
            
            let catFrame = MerchantCategoryFrame.create(
                (0, 0),
                size - (8, 0),
                Spacing,
                self,
                players[consoleplayer].mo,
                category,
                items
            );
            catFrames.push(catFrame);
            catFrame.pack(shopContent);

            shopContent.setHeight(shopContent.getHeight() + catFrame.getHeight() + shopContent.getPadding());
            totalItems += items.size();
        }

        Array<MerchantItemBtn> itemBtns;
        foreach (catFrame : catFrames) foreach (itemFrame : catFrame.itemFrames) itemBtns.push(itemFrame.itemBtn);

        let focusHelper = MerchZFFocusLinkHelper.create(1, totalItems, false, false);
        for (let i = 0; i < totalItems; i++) focusHelper.setElement(i, 0, itemBtns[i]);
        focusHelper.setLinks();

        focusDefaults[MerchZFNavEventType_Down] = itemBtns[0];
        focusDefaults[MerchZFNavEventType_Up] = itemBtns[itemBtns.size() - 1];

        MerchZFScrollContainer.create(
            pos, size + (8, -pos.y),
            8.0, shopContent.getHeight(), 15.0,
            MerchZFBoxTextures.createTexturePixels('Graphics/CommonBackgroundNormal.png', (7,7), (14,14), true, true),
            MerchZFBoxTextures.createTexturePixels('Graphics/CommonBackgroundHover.png', (7,7), (14,14), true, true),
            MerchZFBoxTextures.createTexturePixels('Graphics/CommonBackgroundClick.png', (7,7), (14,14), true, true),
            MerchZFBoxTextures.createTexturePixels('Graphics/CommonBackground.png', (7,7), (14,14), true, true),
            shopContent,
            Spacing
        ).pack(MainFrame);

        MerchantCurrencyFrame.create(
            (MainFrame.getWidth() - 35, 0),
            (35, 35),
            self,
            players[consoleplayer].mo
        ).pack(MainFrame);
    }

    override void ticker() {
        super.ticker();

        let height = 0;
        foreach (catFrame : catFrames) height += catFrame.getHeight() + shopContent.getPadding();

        shopContent.setHeight(height);
    }

    override void setupFocusIndicator() {
        setFocusIndicator(MerchZFBoxImage.create(
            (0, 0), (0, 0),
            MerchZFBoxTextures.CreateTexturePixels('Graphics/FocusIndicator.png', (7, 7), (14, 14), true, true)
        ));
        setFocusPriority(MerchZFFocusPriority_JustAboveFocused);
    }

    override void changeFocusIndicator(MerchZFElement oldFocus, MerchZFElement newFocus) {
        if (newFocus) positionFocusIndicator(newFocus.getPos(), newFocus.getSize());
    }

    override void positionFocusIndicator(Vector2 pos, Vector2 size) {
        getFocusIndicator().setBox(pos, size);
    }
}